! Node types

! Instrinsic scalars
! - logical
! - integer
! - real
! - character(:), allocatable
!
! Intrinsic arrays
! - ArrayWrap(logical(:))
! - ArrayWrap(integer(:))
! - ArrayWrap(real(:))
! - ArrayWrap(character(:))
!
! Generic gFTL containers
! - UnlimitedVector
! - OrderedStringUnlimitedMap
! - IntegerUnlimitedMap
!
! TBD? - Specific gFTL containers
! - Vectors
!   - LogicalVector
!   - IntegerVector
!   - RealVector
!   - StringVector
! - Maps
!   - StringLogicalMap
!   - StringIntegerMap
!   - StringRealMap
!   - StringStringMap
!   - IntegerLogicalMap
!   - IntegerIntegerMap
!   - IntegerRealMap
!   - IntegerStringMap




module Test_Configuration_casting
  use funit
  use fy_Configuration
  use fy_Parser
  use fy_ErrorCodes
  use fy_String
  use fy_OrderedStringUnlimitedMap
  use, intrinsic :: iso_fortran_env, only: INT32
  use, intrinsic :: iso_fortran_env, only: REAL32
  implicit none

contains

  @test
  subroutine test_assign_to_logical_scalar()
    logical :: found

    found = Configuration(scalar=.true.) ! needs overloaded assignment
    @assert_that(found, is(True()))
    
  end subroutine test_assign_to_logical_scalar

  @test
  subroutine test_assign_to_real_scalar()
    real :: found
    real :: expected = 1.23

    found = Configuration(scalar=expected) ! needs overloaded assignment
    @assert_that(found,is(equal_to(expected)))
    
  end subroutine test_assign_to_real_scalar
  
  @test
  subroutine test_assign_to_integer_scalar()
    integer :: found

    found = Configuration(scalar=1) ! needs overloaded assignment
    @assert_that(found,is(equal_to(1)))
    
    found = Configuration(scalar=2) ! needs overloaded assignment
    @assert_that(found,is(equal_to(2)))

  end subroutine test_assign_to_integer_scalar

  @test
  ! Type-mismatch
  subroutine test_assign_to_integer_fail_set_default()
    integer :: found
    real :: expected = 1.23

    found = Configuration(scalar=expected)
    @assert_that(found,is(equal_to(-huge(1_INT32))))

  end subroutine test_assign_to_integer_fail_set_default

  @test
  subroutine test_assign_to_string_scalar()
    type(Configuration), target :: c
    character(:), allocatable :: found
    character(*), parameter :: expected = 'dog'

    c = Configuration(scalar=String(expected))
    found = c ! overloaded assignment
#ifdef __GFORTRAN__
    @assertEqual(found, expected)
#else
    @assert_that(found, is(equal_to(expected)))
#endif
  end subroutine test_assign_to_string_scalar


  @test
  subroutine test_assign_to_logical_array()
    logical, allocatable :: found(:)
    
    found = Configuration(array=[.true.,.false.]) ! needs overloaded assignment
    @assert_that(found,is(equal_to([.true.,.false.])))
    
  end subroutine test_assign_to_logical_array

  @test
  subroutine test_assign_to_logical_array_from_unlimited_vector()
     use gFTL_UnlimitedVector
    logical, allocatable :: found(:)
    type (UnlimitedVector) :: v

    class(*), pointer :: node
    type(Configuration), target :: c


    ! This way does work
    c = Configuration(scalar=v)
    call c%get_node_at_selector(node)
    select type (node)
    type is (UnlimitedVector)
       call node%push_back(.false.)
       call node%push_back(.true.)
       call node%push_back(.false.)
    end select
    found = c
    
    @assert_that(found,is(equal_to([.false.,.true.,.false.])))
    
  end subroutine test_assign_to_logical_array_from_unlimited_vector

  @test
  subroutine test_assign_to_logical_array_fail()
     use gFTL_UnlimitedVector
    logical, allocatable :: found(:)
    type (UnlimitedVector) :: v
    class(*), pointer :: node
    type(Configuration), target :: c

    
    ! type mismatch
    c = Configuration(scalar=v)
    call c%get_node_at_selector(node)
    select type (node)
    type is (UnlimitedVector)
       call node%push_back(1.)
       call node%push_back(3.)
    end select
    found = c
    @assert_that(found, is(empty_array()))
    
    ! category mismatch
    deallocate(found)
    found = Configuration(scalar=1)
    @assert_that(found, is(empty_array()))

    ! mixed types in unlimited
    c = Configuration(scalar=v)
    call c%get_node_at_selector(node)
    select type (node)
    type is (UnlimitedVector)
       call node%push_back(3)
       call node%push_back(.true.)
    end select
    found = c
    @assert_that(found, is(empty_array()))
    
  end subroutine test_assign_to_logical_array_fail
  
  
  @test
  subroutine test_assign_to_integer_array()
     use gFTL_UnlimitedVector
    integer, allocatable :: found(:)
    type (UnlimitedVector) :: v
    class(*), pointer :: node
    type(Configuration), target :: c

    c = Configuration(v)
    call c%get_node_at_selector(node)
    select type (node)
    type is (UnlimitedVector)
       call node%push_back(1)
       call node%push_back(3)
    end select
    
    found = c ! needs overloaded assignment
    @assert_that(found,is(equal_to([1,3])))
    
  end subroutine test_assign_to_integer_array

  @test
  subroutine test_assign_to_integer_array_from_unlimited_vector()
     use gFTL_UnlimitedVector
    integer, allocatable :: found(:)
    type (UnlimitedVector) :: v

    class(*), pointer :: node
    type(Configuration), target :: c

    c = Configuration(scalar=v)
    call c%get_node_at_selector(node)
    select type (node)
    type is (UnlimitedVector)
       call node%push_back(3)
       call node%push_back(2)
       call node%push_back(1)
    end select
    found = c

    @assert_that(found,is(equal_to([3,2,1])))
    
  end subroutine test_assign_to_integer_array_from_unlimited_vector

  @test
  subroutine test_assign_to_integer_array_fail()
     use gFTL_UnlimitedVector
    integer, allocatable :: found(:)
    type (UnlimitedVector) :: v

    class(*), pointer :: node
    type(Configuration), target :: c


    ! type mismatch
    c = Configuration(scalar=v)
    call c%get_node_at_selector(node)
    select type (node)
    type is (UnlimitedVector)
       call node%push_back(1.)
       call node%push_back(2.)
    end select
    found = c
    @assert_that(found, is(empty_array()))
    deallocate(found)
    
    ! category mismatch
    found = Configuration(scalar=1)
    @assert_that(found, is(empty_array()))
    deallocate(found)

    ! mixed types in unlimited
    c = Configuration(scalar=v)
    call c%get_node_at_selector(node)
    select type (node)
    type is (UnlimitedVector)
       call node%push_back(3)
       call node%push_back(2.)
    end select
    found = c
    @assert_that(found, is(empty_array()))
    
  end subroutine test_assign_to_integer_array_fail
  
  
  @test
  subroutine test_assign_to_real_array()
    real, allocatable :: found(:)

    found = Configuration(array=[1.,3.]) ! needs overloaded assignment
    @assert_that(found,is(equal_to([1.,3.])))
    
  end subroutine test_assign_to_real_array

  @test
  subroutine test_assign_to_real_array_from_unlimited_vector()
     use gFTL_UnlimitedVector
    real, allocatable :: found(:)
    type (UnlimitedVector) :: v

    class(*), pointer :: node
    type(Configuration), target :: c

    c = Configuration(scalar=v)
    call c%get_node_at_selector(node)
    select type (node)
    type is (UnlimitedVector)
       call node%push_back(3.)
       call node%push_back(2.)
       call node%push_back(1.)
    end select
    found = c
    
    @assert_that(found,is(equal_to([3.,2.,1.])))
      
  end subroutine test_assign_to_real_array_from_unlimited_vector

  @test
  subroutine test_assign_to_real_array_fail()
    real, allocatable :: found(:)

    ! type mismatch
    found = Configuration(array=[1,3])
    @assert_that(found, is(empty_array()))
    
    ! category mismatch
    deallocate(found)
    found = Configuration(scalar=1)
    @assert_that(found, is(empty_array()))

  end subroutine test_assign_to_real_array_fail
  

#ifndef __GFORTRAN__
  @test(ifndef=__GFORTRAN__)
  subroutine test_assign_to_string_array()
    character(:), allocatable :: found(:)
    
    found = Configuration(array=[character(4) :: 'cat', 'food'])
    @assert_that(found,is(equal_to(['cat ','food'])))
    
  end subroutine test_assign_to_string_array
#endif

  ! Support varying length strings with StringVector
#ifndef __GFORTRAN__
  @test(ifndef=__GFORTRAN__)
  subroutine test_assign_to_string_vector()
     use gFTL_UnlimitedVector
    use gFTL_StringVector
    type (StringVector) :: found
    type (UnlimitedVector) :: expected
    integer :: n
    integer :: sz

    found = Configuration(array=[character(4) :: 'cat', 'food'])
    @assert_that(int(found%size()), is(equal_to(2)))
    @assert_that(found%at(1), is(equal_to('cat ')))
    @assert_that(found%at(2), is(equal_to('food')))

    ! This way does not ...
    call expected%push_back(String('cat'))
    call expected%push_back(String('food'))

    found = Configuration(scalar=expected)
    sz = found%size()
    @assert_that(sz, is(equal_to(2)))
    @assert_that(found%at(1), is(equal_to('cat')))
    @assert_that(found%at(2), is(equal_to('food')))
  end subroutine test_assign_to_string_vector
#endif

  @test
  subroutine test_assign_to_string_array_from_unlimited_vector()
     use gFTL_UnlimitedVector
    character(:), allocatable :: found(:)
    type (UnlimitedVector) :: v

    class(*), pointer :: node
    type(Configuration), target :: c

    c = Configuration(scalar=v)
    call c%get_node_at_selector(node)
    select type (node)
    type is (UnlimitedVector)
       call node%push_back(String('cat'))
       call node%push_back(String('food'))
       call node%push_back(String(''))
    end select
    found = c
    
#ifdef __GFORTRAN__
    @assertEqual(found(1), 'cat ')
    @assertEqual(found(2), 'food')
    @assertEqual(found(3), '    ')
#else
    @assert_that(found,is(equal_to(['cat ','food','    '])))
#endif    
  end subroutine test_assign_to_string_array_from_unlimited_vector

  @test
  subroutine test_assign_to_string_array_fail()
     use gFTL_UnlimitedVector
    character(:), allocatable :: found(:)
    type (UnlimitedVector) :: v
    class(*), pointer :: node
    type(Configuration), target :: c


    ! type mismatch
    found = Configuration(array=[1,3])
    @assert_that(found, is(empty_array()))
    
    ! partial mismatch
    c = Configuration(scalar=v)
    call c%get_node_at_selector(node)
    select type (node)
    type is (UnlimitedVector)
       call node%push_back('a')
       call node%push_back(1)
    end select
    found = c

    @assert_that(found, is(empty_array()))
    
    ! category mismatch
    deallocate(found)
    found = Configuration(scalar=1)
    @assert_that(found, is(empty_array()))

  end subroutine test_assign_to_string_array_fail

  @test
  subroutine test_at_key_not_a_map()
    type(Configuration), target :: c
    type(Configuration) :: sub

    c = Configuration(scalar=1)
    sub = c%at('non-existent-key') ! should "fail"
    @assert_that(sub%is_none(), is(true()))
    
  end subroutine test_at_key_not_a_map

  @test
  subroutine test_at_key_not_a_key()
    type(Configuration), target :: c
    type(Configuration) :: sub

    class(*), pointer :: node

    c = Configuration(OrderedStringUnlimitedMap())
    call c%get_node_at_selector(node)
    select type (node)
    type is (OrderedStringUnlimitedMap)
       call node%insert('a',1)
       call node%insert('b',.true.)
    end select

    sub = c%at('non-existent-key') ! should "fail"
    @assert_that(sub%is_none(), is(true()))
    
  end subroutine test_at_key_not_a_key

  @test
  subroutine test_at_key_integer()
    type(Configuration),target :: c
    integer :: found
    
    class(*), pointer :: node

    c = Configuration(OrderedStringUnlimitedMap())
    call c%get_node_at_selector(node)
    select type (node)
    type is (OrderedStringUnlimitedMap)
       call node%insert('a',1)
       call node%insert('b',2)
    end select

    found = c%at('b')
    @assert_that(found,is(equal_to(2)))
  end subroutine test_at_key_integer

  @test
  subroutine test_at_key_logical()
    type(Configuration), target :: c
    logical :: found
    
    class(*), pointer :: node

    c = Configuration(OrderedStringUnlimitedMap())
    call c%get_node_at_selector(node)
    select type (node)
    type is (OrderedStringUnlimitedMap)
       call node%insert('a',1)
       call node%insert('b',.false.)
    end select

    found = c%at('b')
    @assert_that(found,is(false()))
  end subroutine test_at_key_logical

  @test
  subroutine test_at_key_map()
    type(Configuration), target :: c
    type(Configuration) :: sub
    type(OrderedStringUnlimitedMap) :: found_map

    class(*), pointer :: node
    class(*), pointer :: node2
    integer :: n

    c = Configuration(OrderedStringUnlimitedMap())
    call c%get_node_at_selector(node)
    select type (node)
    type is (OrderedStringUnlimitedMap)
       call node%insert('a',1)
       call node%insert('b',OrderedStringUnlimitedMap())
       node2 => node%at('b')
       select type (node2)
       type is (OrderedStringUnlimitedMap)
          call node2%insert('triangle',3)
          call node2%insert('quadrilateral',4)
       end select
    end select
    call c%get(sub,'b')
    n = sub%at('triangle')
    @assert_that(n,is(equal_to(3)))

  end subroutine test_at_key_map

  @test
  subroutine test_at_index_not_a_vector()
    type(Configuration), target :: c
    type(Configuration) :: sub

    c = Configuration(scalar=1)
    sub = c%at(5) ! should "fail"
    @assert_that(sub%is_none(), is(true()))
    
  end subroutine test_at_index_not_a_vector
  
  @test
  subroutine test_at_index_out_of_range()
     use gFTL_UnlimitedVector
    type(Configuration), target :: c
    type(Configuration) :: sub
    type(UnlimitedVector) :: v

    class(*), pointer :: node
    
    ! This way does work
    c = Configuration(scalar=v)
    call c%get_node_at_selector(node)
    select type (node)
    type is (UnlimitedVector)
       call node%push_back(1)
       call node%push_back(7.)
    end select

    sub = c%at(5) ! should "fail"
    @assert_that(sub%is_none(), is(true()))
    
  end subroutine test_at_index_out_of_range
  
  @test
  subroutine test_at_index()
     use gFTL_UnlimitedVector
    type(Configuration), target :: c
    type(UnlimitedVector) :: vector
    logical :: found_logical
    integer :: found_integer
    real :: found_real
    character(:), allocatable :: found_string
    class(*), pointer :: node

    c = Configuration(UnlimitedVector())
    call c%get_node_at_selector(node)
    select type (node)
    type is (UnlimitedVector)
       call node%push_back(.true.)
       call node%push_back(1)
       call node%push_back(1.234)
       call node%push_back(String('foo'))
    end select

    found_logical = c%at(1)
    @assert_that(found_logical,is(true()))

    found_integer = c%at(2)
    @assert_that(found_integer,is(equal_to(1)))

    found_real = c%at(3)
    @assert_that(found_real,is(equal_to(1.234)))

    found_string = c%at(4)
#ifdef __GFORTRAN__
    @assertEqual(found_string, 'foo')
#else
    @assert_that(found_string,is(equal_to('foo')))
#endif

  end subroutine test_at_index

  @test
  subroutine test_at_index_map()
     use gFTL_UnlimitedVector
    type(Configuration), target :: c
    type(OrderedStringUnlimitedMap) :: found_map

    class(*), pointer :: node, node2
    integer :: n

    c = Configuration(UnlimitedVector())
    call c%get_node_at_selector(node)

    select type (node)
    type is (UnlimitedVector)
       call node%push_back(1)
       call node%push_back(OrderedStringUnlimitedMap())
       node2 => node%back()
       select type (node2)
       type is (OrderedStringUnlimitedMap)
          call node2%insert('triangle',3)
          call node2%insert('quadrilateral',4)
       end select
       call node%push_back(2.)
    end select

    n = c .at. 2 .at. 'triangle'
    @assert_that(n,is(equal_to(3)))
    n = c .at. 2 .at. 'quadrilateral'
    @assert_that(n,is(equal_to(4)))

  end subroutine test_at_index_map


  ! The following test is to explore compiler issue encountered with
  ! GFortran while processing simple.yaml in the examples.

  @test
  subroutine test_simple_example()
     use gFTL_UnlimitedVector
    type(Configuration), target :: c
    type(OrderedStringUnlimitedMap) :: found_map

    class(*), pointer :: node, node2
    integer :: v1, v2, v3
    logical :: is_present
    integer :: status

    c = Configuration(OrderedStringUnlimitedMap())
    call c%get_node_at_selector(node)

    select type (node)
    type is (OrderedStringUnlimitedMap)
       call node%insert('x', 1.234)
       call node%insert('name', 'Fred')
       call node%insert('flag', .true.)

       call node%insert('sequence_a', UnlimitedVector())
       node2 => node%at('sequence_a')
       select type (node2)
       type is (unlimitedVector)
          call node2%push_back(1)
          call node2%push_back(2)
          call node2%push_back(3)
          call node2%push_back(4)
       end select

       call node%insert('sequence_b', Unlimitedvector())
       node2 => node%at('sequence_a')
       select type (node2)
       type is (unlimitedVector)
          call node2%push_back(1)
          call node2%push_back(2)
          call node2%push_back(3)
          call node2%push_back(4)
       end select

       call node%insert('mapping_b', OrderedStringUnlimitedMap())
       node2 => node%at('mapping_b')
       select type (node2)
       type is(OrderedStringUnlimitedMap)
          call node2%insert('v1', 7)
          call node2%insert('v2', 8)
       end select

       call node%insert('mapping_a', OrderedStringUnlimitedMap())
       node2 => node%at('mapping_a')
       select type (node2)
       type is(OrderedStringUnlimitedMap)
          call node2%insert('v1', 7)
          call node2%insert('v2', 8)
       end select

    end select

    ! This works:
    v1 = c .at. 'mapping_b' .at. 'v1'
    @assert_that(v1,is(equal_to(7)))
    v2 = c .at. 'mapping_b' .at. 'v2'
    @assert_that(v2,is(equal_to(8)))


    block
      type(Configuration) :: cc

      cc = c%at('mapping_b', 'v1')
      v1 = cc
      @assert_that(v1,is(equal_to(7)))
    end block

    ! but this does crashes:
    v1 = c%at('mapping_b', 'v1')
    @assert_that(v1,is(equal_to(7)))
    v2 = c%at('mapping_b', 'v2')
    @assert_that(v2,is(equal_to(8)))

    ! And this:
   v1 = -1
   call c%get(v1, 'mapping_b', 'v1', is_present=is_present, rc=status)
   @assert_that(v1, is(equal_to(7)))
   @assert_that(is_present, is(true()))
   @assert_that(status, is(equal_to(YAFYAML_SUCCESS)))

   ! As does this
   call c%get(v3, 'mapping_b', 'v3', default=17, is_present=is_present, rc=status)

   @assert_that(v3, is(equal_to(17)))
   @assert_that(is_present, is(false()))
    
 end subroutine test_simple_example

end module Test_Configuration_casting
