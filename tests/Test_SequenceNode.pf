module Test_SequenceNode
   use funit
   use fy_Nodes
   use fy_ErrorCodes
   use fy_NodeVector
   use, intrinsic :: iso_fortran_env, only: INT32, INT64
   use, intrinsic :: iso_fortran_env, only: REAL32, REAL64
   implicit none

contains

   @test
   subroutine test_is_sequence()
      class(AbstractNode), target, allocatable :: node
      type(NodeVector), pointer :: sequence
      type(SequenceNode), target :: s_node
      logical :: flag

      s_node = SequenceNode()
      @assertTrue(s_node%is_sequence())

      allocate(node,source=SequenceNode())
      flag = node%is_sequence()
      @assertTrue(flag)

      node = IntNode(1_INT32)
      @assertFalse(node%is_sequence())

   end subroutine test_is_sequence

   @test
   subroutine test_casting_succeed()
      class(AbstractNode), target, allocatable :: node
      
      type(NodeVector), pointer :: s_ptr
      character(:), allocatable :: err_msg
      integer :: rc

      allocate(node, source=SequenceNode())
      err_msg = 'foo'
      s_ptr => to_sequence(node, err_msg=err_msg, rc=rc) ! emtpy
      call s_ptr%push_back(IntNode(7_INT32))
      s_ptr => to_sequence(node, err_msg=err_msg, rc=rc) ! emtpy
      
      @assert_that(int(s_ptr%size()), is(1))
      @assert_that(rc, is(YAFYAML_SUCCESS))
      ! err_msg not changed if no error
      @assert_that(err_msg, is('foo'))
   end subroutine test_casting_succeed

   @test
   subroutine test_casting_fail()
      type(IntNode) :: i_node
      type(NodeVector), pointer :: s_ptr
      integer, parameter :: MAXLEN=128

      character(MAXLEN) :: err_msg
      character(:), allocatable :: expected
      integer :: rc

      i_node = IntNode(1_INT32)
      s_ptr => to_sequence(i_node, err_msg=err_msg, rc=rc)
      @assertExceptionRaised('Type of request does not match type in config.')
      @assert_that(int(s_ptr%size()), is(0))
      @assert_that(rc, is(YAFYAML_TYPE_MISMATCH))
      expected = error_message(YAFYAML_TYPE_MISMATCH)
      @assertEqual(expected, err_msg)

   end subroutine test_casting_fail


   @test
   ! Empty sequences are ==, so not <
   subroutine test_less_than_trivial()
      type(SequenceNode), target :: s
      type(NodeVector), pointer :: sequence

      s = SequenceNode()
      sequence => s%sequence()
      call sequence%push_back(IntNode(1_INT32))
      call sequence%push_back(BoolNode(.false.))

      @assertFalse(s < s)

   end subroutine test_less_than_trivial
   
   @test
   subroutine test_less_than_first_differs
      type(SequenceNode), target :: s1, s2
      type(NodeVector), pointer :: seq1, seq2

      seq1 => to_sequence(s1)
      seq2 => to_sequence(s2)

      call seq1%push_back(IntNode(1_INT32))
      call seq2%push_back(IntNode(2_INT32))

      print*,__FILE__,__LINE__
      @assertTrue(s1 < s2)
      print*,__FILE__,__LINE__
      @assertFalse(s2 < s1)

   end subroutine test_less_than_first_differs
   
   @test
   subroutine test_less_than_second_differs
      type(SequenceNode), target :: s1, s2
      type(NodeVector), pointer :: seq1, seq2

      seq1 => to_sequence(s1)
      seq2 => to_sequence(s2)

      call seq1%push_back(IntNode(1_INT32))
      call seq1%push_back(FloatNode(1._REAL32))

      call seq2%push_back(IntNode(1_INT32))
      call seq2%push_back(FloatNode(2._REAL32))

      @assertTrue(s1 < s2)
      @assertFalse(s2 < s1)

      call seq2%pop_back()
      call seq2%push_back(IntNode(2_INT32))  ! int < float

      @assertFalse(s1 < s2)
      @assertTrue(s2 < s1)
      
   end subroutine test_less_than_second_differs
   
   @test
   subroutine test_less_than_second_longer
      type(SequenceNode), target :: s1, s2
      type(NodeVector), pointer :: seq1, seq2

      seq1 => to_sequence(s1)
      seq2 => to_sequence(s2)

      call seq1%push_back(IntNode(1_INT32))
      call seq1%push_back(FloatNode(2._REAL32))

      call seq2%push_back(IntNode(1_INT32))
      call seq2%push_back(FloatNode(2._REAL32))
      call seq2%push_back(FloatNode(3._REAL32)) 

      @assertTrue(s1 < s2)
      @assertFalse(s2 < s1)
   end subroutine test_less_than_second_longer
   @test
   subroutine test_less_than_diff_type
      type(BoolNode) :: flag
      type(IntNode) :: i
      type(StringNode) :: str
      type(FloatNode) :: x
      type(SequenceNode) :: seq
      type(MappingNode) :: m

      flag = BoolNode(.false.)
      i = IntNode(1)
      x = FloatNode(1.)
      str = StringNode('cat')

      @assertFalse(seq < flag)
      @assertFalse(seq < i)
      @assertFalse(seq < str)
      @assertFalse(seq < x)
      @assertTrue(seq < m)

   end subroutine test_less_than_diff_type
   
end module Test_SequenceNode
