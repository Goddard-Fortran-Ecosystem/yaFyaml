module Test_Parser
   use funit
   use yafyaml
   use fy_Parser
   implicit none

contains

   @test
   subroutine test_single_scalar()
      type(Parser) :: p
      type(Configuration) :: cfg
      character(:), allocatable :: scalar

      p = Parser()
      cfg = p%load(EscapedTextStream("--- a\n..."))

      scalar = cfg
#ifdef __GFORTRAN__
#else
      @assert_that(scalar, is("a"))
#endif
   end subroutine test_single_scalar

   @test
   subroutine test_single_flow_sequence()
      type(Parser) :: p
      type(Configuration) :: cfg
      logical, allocatable :: flags(:)

      p = Parser()
      cfg = p%load(EscapedTextStream("---\n [true, false, true]\n..."))

      flags = cfg
      @assert_that(flags, is(equal_to([.true.,.false.,.true.])))

   end subroutine test_single_flow_sequence

   @test
   subroutine test_single_flow_mapping()
      type(Parser) :: p
      type(Configuration) :: cfg
      logical :: flag

      p = Parser()
      cfg = p%load(EscapedTextStream("---\n {a: true, b: false}\n..."))

      flag = cfg%at("a")
      @assert_that(flag, is(equal_to(.true.)))

      flag = cfg%at("b")
      @assert_that(flag, is(equal_to(.false.)))


   end subroutine test_single_flow_mapping


   @test
   subroutine test_single_block_sequence()
      type(Parser) :: p
      type(Configuration) :: cfg
      logical, allocatable :: flags(:)

      p = Parser()
      cfg = p%load(EscapedTextStream("---\n - true \n - false \n - true \n..."))

      flags = cfg
      @assert_that(flags, is(equal_to([.true.,.false.,.true.])))

   end subroutine test_single_block_sequence

   @test
   subroutine test_nested_block_sequence()
      type(Parser) :: p
      type(Configuration) :: cfg
      type(Configuration) :: sub

      integer, allocatable :: ints(:)

      p = Parser()
      cfg = p%load(EscapedTextStream("---\n - \n    - 1 \n    - 2 \n - \n    - 3 \n    - 4 \n..."))
      !                               0123 0123 012345678 012345678 0123 012345678 012345678 012

      sub = cfg%at(1)

      ints = sub
      @assert_that(ints, is(equal_to([1,2])))

      sub = cfg%at(2)
      ints = sub
      @assert_that(ints, is(equal_to([3,4])))

   end subroutine test_nested_block_sequence

   @test
   subroutine test_nested_block_mapping_sequence()
      type(Parser) :: p
      type(Configuration) :: cfg
      type(Configuration) :: sub

      integer, allocatable :: ints(:)

      p = Parser()
      cfg = p%load(EscapedTextStream("---\n cat: \n    - 1 \n    - 2 \n dog: \n    - 3 \n    - 4 \n..."))
      !                               0123 0123456 012345678 012345678 0123567 012345678 012345678 012

      sub = cfg%at('cat')
      ints = sub
      @assert_that(ints, is(equal_to([1,2])))

      sub = cfg%at('dog')
      ints = sub
      @assert_that(ints, is(equal_to([3,4])))

   end subroutine test_nested_block_mapping_sequence

   @test
   subroutine test_nested_mapping_block_flow()
      type(Parser) :: p
      type(Configuration) :: cfg
      type(Configuration) :: sub

      integer :: v1, v2

      p = Parser()
      cfg = p%load(EscapedTextStream("---\n mapping: { v1: 7, v2: 8 } \n..."))

      sub = cfg%at('mapping')

      v1 = sub%at('v1')
      @assert_that(v1, is(equal_to(7)))

      v2 = sub%at('v2')
      @assert_that(v2, is(equal_to(8)))

   end subroutine test_nested_mapping_block_flow

   @test
   subroutine test_pflogger_reproducer()
      type(Parser) :: p
      type(Configuration) :: cfg

      p = Parser()
      cfg = p%load(EscapedTextStream("format: --- \n"))

   end subroutine test_pflogger_reproducer


   @test
   subroutine test_pflogger_reproducer2()
      type(Parser) :: p
      type(Configuration) :: cfg

      p = Parser()
      cfg = p%load(EscapedTextStream( &
           & " B: {a: '---' , b: hello}\n"))

   end subroutine test_pflogger_reproducer2


   ! Another pflogger use case that breaks
   ! gfortran
   @test
   subroutine test_mapping_with_strings
      type(Configuration), target :: c
      type(Parser) :: p
      class(*), pointer :: node
      
      c = Configuration(OrderedStringUnlimitedMap())
      call c%get_node_at_selector(node)

      select type (node)
      type is (OrderedStringUnlimitedMap)
         call node%insert('format', "%(asctime)a %(message)a")
         call node%insert('datemft', 'date')
      end select

         
         p = Parser()
         c = p%load(EscapedTextStream( &
           & ' format: "%(asctime)a %(message)a"\n' // &
           & " datefmt: date \n"))

   end subroutine test_mapping_with_strings

   @test
   subroutine test_simple_anchor()
      type(Parser) :: p
      type(Configuration) :: cfg
      type(Configuration) :: sub

      integer :: i_a, i_b

      p = Parser()
      cfg = p%load(EscapedTextStream( &
           & "---\n" // &
           & " A: &anchor \n" // &
           & "    i: 1 \n" // &
           & " B: *anchor \n" // &
           & "..."))

      i_a = cfg%at('A', 'i')
      @assert_that(i_a, is(equal_to(1)))
      
      i_b = cfg%at('B', 'i')
      @assert_that(i_b, is(equal_to(1)))
      

   end subroutine test_simple_anchor

   ! Reproducer for issue #13
   @test
   subroutine test_quoted_integer()
      type(Parser) :: p
      type(Configuration) :: cfg

      character(:), allocatable :: s

      p = Parser()
      cfg = p%load(EscapedTextStream(' key1: "2004" \n'))

      call cfg%get(s,"key1", default="ABCDEFG")

      @assert_that(s, is(equal_to("2004")))
      print*,"hi"
   end subroutine test_quoted_integer


end module Test_Parser

